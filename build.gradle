plugins {
    id 'java'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.bunubbv'
version = '2.3.4'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT'
    compileOnly 'net.dmulloy2:ProtocolLib:5.4.0'
    implementation 'org.xerial:sqlite-jdbc:3.51.1.0'
}

tasks {
    runServer {
        minecraftVersion '1.19'
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.shadowJar {
    archiveClassifier.set('')
}

tasks.build {
    dependsOn tasks.shadowJar
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
